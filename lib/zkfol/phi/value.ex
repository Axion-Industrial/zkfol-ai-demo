defmodule Zkfol.Phi.Value do
  @moduledoc """
  I am the operations the unifier needs on a value: a place, a list of them, a handed
  count, a passed relation, or nothing yet.

  ### Public API

  - `shaped/2`: read an element whose bank's shape has since been learned.
  - `scalar/1`: the arithmetic reading of a value.
  - `elements/2`: its elements for a primitive operation.
  - `frame/2`, `unframe/2`: express values across a call's column frame.
  """

  alias Zkfol.Ast
  alias Zkfol.Phi.Place

  require Place

  @typedoc "A scalar expression can name an element whose shape its constraint will resolve."
  @type scalar :: Ast.poly(Ast.ep_leaf() | {:reify, Ast.pred()} | Place.t())

  @typedoc "What a name holds: a place, or a scalar expression over places."
  @type t :: Place.t() | scalar()

  @typedoc "Where a call reaches: an affine frame of the column, or a pointer cell."
  @type frame :: Ast.address()

  @doc "I expose a finite list as the values a primitive consumes."
  @spec elements(t(), Place.known()) :: t()
  def elements(laid, known) when Place.is_laid(laid), do: Place.cells(laid, known)
  def elements({:pair, h, t}, known), do: [elements(h, known) | elements(t, known)]
  def elements([h | t], known), do: [elements(h, known) | elements(t, known)]
  def elements({:count, _q, form}, _known), do: form
  def elements(form, _known), do: form

  @doc "I express a callee value at its call address in the caller."
  @spec frame(t(), frame()) :: t()
  def frame({:fresh, ref}, frame), do: frame({:cell, ref}, frame)
  def frame(form, {:at, :x, 1, 0}), do: form
  def frame({:node, id}, frame), do: {:node, frame(id, frame)}
  def frame({:pair, h, t}, frame), do: {:pair, frame(h, frame), frame(t, frame)}
  def frame([h | t], frame), do: [frame(h, frame) | frame(t, frame)]
  def frame({:count, _q, form}, frame), do: frame(form, frame)
  def frame({:rel, _p, _f} = passed, _frame), do: passed
  def frame(form, _frame) when is_integer(form) or form in [:fresh, []], do: form

  def frame(place, frame) when elem(place, 0) in [:along, :held, :across] do
    case Place.framed(place, frame) do
      :unreached -> throw({:refused, {:unliftable_term, %{term: place}}})
      reached -> reached
    end
  end

  def frame(form, frame) do
    Ast.postwalk(form, fn
      :x ->
        Ast.naming(frame)

      {:add, a, b} ->
        Ast.add(a, b)

      cell when is_tuple(cell) and elem(cell, 0) == :cell ->
        {row, address} = Ast.read(cell)

        case Ast.reframe(address, frame) do
          nil -> throw({:refused, {:unliftable_term, %{term: cell}}})
          {:at, base, m, a} -> Ast.at(row, base, m, a)
        end

      node ->
        node
    end)
  end

  @doc """
  I express the values a callee can retain in its own column frame. A list the frame
  cannot express keeps the caller's address; the callee re-heads it at its own extent.
  """
  @spec unframe(t(), frame()) :: t()
  def unframe(form, {:at, :x, 1, 0}), do: form

  def unframe(place = {:across, _, _, _}, frame) do
    case Place.unframed(place, frame) do
      :unreached -> throw({:refused, {:unliftable_term, %{term: place}}})
      reached -> reached
    end
  end

  def unframe(node = {:node, _id}, _frame), do: node

  def unframe(laid, frame) when Place.is_laid(laid) do
    case Place.unframed(laid, frame) do
      :unreached -> laid
      unframed -> unframed
    end
  end

  def unframe({:count, _q, _cell} = count, _frame), do: count

  def unframe(form, _frame) when is_list(form) or form == :fresh or elem(form, 0) == :rel,
    do: form

  def unframe(form, frame) do
    case Place.affine(form) do
      {0, q} ->
        q

      {m, a} ->
        case Ast.unframe(Ast.address(:x, m, a), frame) do
          nil -> :fresh
          local -> Ast.naming(local)
        end

      nil ->
        :fresh
    end
  end

  @doc "I read an element whose bank's shape has since been learned, wherever it stands."
  @spec shaped(t() | Ast.pred(scalar()), Place.known()) :: t() | Ast.pred(scalar())
  def shaped(element = {:across, row = {bank, first}, {:at, base, m, a}, n}, known) do
    case Map.get(known, row, :unknown) do
      :scalar when n == 0 ->
        Ast.at(row, base, m, a)

      {:list, {0, width}, :scalar} ->
        Enum.map(n..(width - 1)//1, &Ast.at({bank, first + &1}, base, m, a))

      _unresolved ->
        element
    end
  end

  def shaped({:pair, h, t}, known), do: {:pair, shaped(h, known), shaped(t, known)}
  def shaped([h | t], known), do: [shaped(h, known) | shaped(t, known)]
  def shaped({:node, id}, known), do: {:node, shaped(id, known)}
  def shaped(laid, _known) when Place.is_laid(laid), do: laid

  def shaped(value, known) when is_tuple(value),
    do: value |> Tuple.to_list() |> Enum.map(&shaped(&1, known)) |> List.to_tuple()

  def shaped(value, _known), do: value

  @doc "I read a scalar value, refusing structure where arithmetic requires a number."
  @spec scalar(t()) :: scalar()
  def scalar({:node, id}), do: Place.read(:value, id)
  def scalar({:count, _q, form}), do: form
  def scalar({:fresh, ref}), do: {:cell, ref}

  def scalar(form)
      when is_list(form) or
             (is_tuple(form) and elem(form, 0) in [:along, :pair, :rel]),
      do: throw({:refused, {:unliftable_term, %{term: form}}})

  def scalar(form), do: form
end
